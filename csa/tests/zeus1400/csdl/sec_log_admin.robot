# $Id: //prod/main/sarf_centos/tests/zeus1381/csdl/sec_log_admin.txt#3 $
# $Date: 2020/11/06 $
# $Author: mrmohank $

*** Settings ***
Library      Collections
Resource     sma/global_sma.txt
Resource     regression.txt
Resource     sma/csdlresource.txt

Force Tags   csdl
Suite Setup   CSDL Suite Setup
Suite Teardown  CSDL Suite Teardown

*** Variables ***
${configuration_history_log_name}  config_history
${alert_email_receipient}  test@qa.com
${config_history_path}  /data/pub/config_history

*** Keywords ***
Get config file contents
    [Arguments]  ${configuration_files_list}

    ${config_file_name}=  Evaluate  re.search('${configuration_history_log_name}..*T[0-9]+.s', """${configuration_files_list}""").group(0)  modules=re
    ${config_file_cat_command}=  Catenate  cat  ${config_file_name}
    ${config_file_details}=  Run On DUT  cd ${config_history_path} && ${config_file_cat_command}
    [Return]  ${config_file_details}

*** Test Cases ***
Tvh1340620c
    [Documentation]  Tvh1340620c-SEC-LOG-ADMIN: Log administrative access
        ...  FLOW DETAILS
        ...  Enable config_History Logs from Log subscription
        ...  Go to System Administration and add an alert setting.
        ...  Go to SMA CLI -> cd /data/pub/config_history
        ...  List all the files and check files with '.s' extension
        ...  Verify information on who has modified the configuration, at what time , from which system  with XML format information.

    [Tags]  cli  Tvh1340620c
    [Setup]  Run keywords  Run keyword and ignore error  Login To DUT  ${DUT_ADMIN}  ${DUT_ADMIN_SSW_PASSWORD}
    ...  AND  Run keyword and ignore error  Log subscriptions delete  ${configuration_history_log_name}
    ...  AND  Run keyword and ignore error  Alerts Delete recipient  ${alert_email_receipient}
    ...  AND  Run On DUT  rm -rf /data/pub/config_history
    ...  AND  Log Subscriptions Add Log  ${sma_log_types.CONF_HISTORY}  ${configuration_history_log_name}
    ...  AND  Commit Changes


    Alerts Add Recipient  ${alert_email_receipient}  all-all
    Commit Changes
    Run On DUT  cd ${config_history_path} && rm -rf *.s
    Roll Over Now
    ${configuration_history_files}=  Run On DUT  cd ${config_history_path} && ls -lrt
    Should match regexp  ${configuration_history_files}  .*.s
    ${config_file_contents}=  Get config file contents  ${configuration_history_files}
    Should match regexp  ${config_file_contents}  .*XML generated by configuration change..*
    should match regexp  ${config_file_contents}  .*User: ${DUT_ADMIN}.*
    should match regexp  ${config_file_contents}  .*Email address configuration to send alert messages to.*
    should match regexp  ${config_file_contents}  .*Current Time: .* .* [0-9]+ [0-9]+:[0-9]{2}:[0-9]{2} [0-9]{4}.*
