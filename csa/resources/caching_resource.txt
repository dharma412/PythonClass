# $Id: //prod/main/sarf_centos/resources/caching_resource.txt#1 $
# $DateTime: 2019/03/22 01:36:06 $
# $Author: aminath $

*** Settings ***
Variables  constants.py
Resource   wsa/global.txt

*** Variables ***
${POLICY}        Policy01
${D_POLICY}        DPolicy01

*** Keyword ***
Request And Verify Logs  [Arguments]   ${patt}=${Empty}  ${request}=${Empty}  ${no_entry}=1

     [Documentation]   Arguments:
     ...        patt - Pattern to search in accesslogs \n
     ...        request - Request URL for Curl request \n
     ...        no_entry - number of new access log entries \n
     ...   Keyword for request URL using curl and verify \n
     ...   search patterns. \n
     # Create baseline
     Filter Access Log Create Baseline
     # send request
     ${client_out}  Run  curl ${IPV_PARAM} -m 60 -v -x ${DUT}:3128 -o /dev/null -H Pragma: ${request}
     Log  ${client_out}
     # search pattern in accesslogs
     Filter Access Log Check    ${baseline_accesslog}  ${patt}  ${no_entry}

Block Filetype  [Arguments]   ${filetypes_list}=${Empty}
     [Documentation]   Arguments: \n
     ...    filetypes_list  - List of file types to block \n
     ...   Keyword adds new access policy & edit objects \n
     Access Policies Add   ${POLICY}
     ...  description=Test access policy
     ...  identity=${GLOBAL_IDENTITY}
     Access Policies Edit Objects   ${POLICY}
     ...  setting_type=custom
     ...  file_types=${filetypes_list}

Edit Mcafee Engine  [Arguments]   ${mcafee}=${Empty}
     [Documentation]   Arguments: \n
     ...    mcafee - either ${False} or ${True} to enable or disable mcafee \n
     Wbrs And Anti Malware Edit Settings
     ...    mcafee=${mcafee}

Edit Http Download Size  [Arguments]   ${http_size}=${Empty}
     [Documentation]   Arguments: \n
     ...    http_size - Http download size \n
     ...  This keyword edits http(s) download size \n
     Access Policies Edit Objects
     ...  Global Policy
     ...  setting_type=custom
     ...  http_size=${http_size}

Edit Url Action  [Arguments]   ${action}=${Empty}
     [Documentation]   Arguments: \n
     ...    action - Action for all predefined URL categories under Decryption policy \n
     Decryption Policies Add    ${D_POLICY}
     ...  description=Test Decryption policy
     ...  identities=${GLOBAL_IDENTITY}
     Decryption Policies Edit Url Categories   ${D_POLICY}
     ...  all_predefine_url_action=${action}
