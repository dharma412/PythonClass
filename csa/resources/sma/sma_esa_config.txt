*** Settings ***
Resource     esa/global.txt
Resource     esa/logs_parsing_snippets.txt
Resource     esa/injector.txt
Resource     regression.txt
Resource     sma/global_sma.txt
Resource     sma/esasma.txt

*** Variables ***
${SESSION_TIMEOUT}=  1440
${GETDTIME_FMT_LOCAL} =  %a %b %d %H:%M:%S %Y

*** Keywords ***

Smaesa Suite Setup
    Set Aliases For Appliance Libraries
    Set Appliance Under Test to ESA

    DefaultTestSuiteSetup
    ${ESA_PUB_LISTENER}=  Get ESA Listener
    Set Suite Variable  ${ESA_PUB_LISTENER}
    ${ESA_PUB_LISTENER_IP}=  Get ESA Public IP
    Set Suite Variable  ${ESA_PUB_LISTENER_IP}
    ${ESA_PR_LISTENER_IP}=  Get ESA Private IP
    Set Suite Variable  ${ESA_PR_LISTENER_IP}

    Update Config Dynamic Host  dynamic_host=${STAGING_UPDATE_SERVER_FOR_VIRTUAL}
    Update Config Validate Certificates  validate_certificates=No
    Commit
    Roll Over Now  logname=updater
    Update Now  force=True
    Reporting Config Setup  enable=yes
    Commit

    Message Tracking Enable  tracking=centralized
    Commit Changes

    Interfaceconfig Edit  Management
    ...  api_http=yes  api_https=yes
    commit

    RAT Recipient Edit  InBoundMail   All  action=Accept

    Admin Access Config Timeout
    ...  timeout_webui=1440
    ...  timeout_cli=1440
    Commit

    Diagnostic Tracking Delete DB  confirm=Yes
    Commit Changes
    Commit
    Null Smtpd Start

     ###### SMA CONFIG ######
    Set Appliance Under Test to SMA
    global_sma.DefaultTestSuiteSetup

    ${SMA_CONFIG_FILE}=  Configuration File Save Config
    Set Suite Variable  ${SMA_CONFIG_FILE}

    Admin Access Config Timeout   timeout_webui=1440  timeout_cli=1440
    Commit
    Diagnostic Reporting Delete DB  confirm=yes
    Diagnostic Tracking Delete DB  confirm=yes
    Commit
    Centralized Email Reporting Enable
    Centralized Email Message Tracking Enable

    Security Appliances Add Email Appliance
    ...  ${ESA}
    ...  ${ESA_IP}
    ...  reporting=${True}
    ...  ssh_credentials=${DUT_ADMIN}:${DUT_ADMIN_SSW_PASSWORD}
    Commit Changes

    Network Access Edit Settings  ${SESSION_TIMEOUT}
    Commit Changes
    Close Browser
    Esaapiconfig Validate  validate_certificate=N
    Commit

Sma Ngui Login
    Set Appliance Under Test to SMA
    SMANGGuiLibrary.Launch Dut Browser
    SMANGGuiLibrary.Login Into Dut  ${DUT_ADMIN}  ${DUT_ADMIN_SSW_PASSWORD}
    SMANGGuiLibrary.View Data For Appliance  ${ESA}

Sma Ngui Teardown
    SMANGGuiLibrary.Clear Tracking Search
    SMANGGuiLibrary.Close Browser

Smaesa Suite Teardown
    Null Smtpd Stop
    Set Appliance Under Test to ESA
    Message Tracking Disable
    DefaultTestSuiteTeardown
    Set Appliance Under Test to SMA
    Run Keyword And Ignore Error  Start CLI Session If Not Open
    Suspend  0
    Reset Config
    Configure SSL For GUI
    Selenium Login
    Configuration File Load Config  ${SMA_CONFIG_FILE}
    Sleep  5
    Commit Changes
    DefaultTestSuiteTeardown

Do Common Testcase Setup
    DefaultTestCaseSetup

    EsaCliLibrary.Smtp Routes New
    ...  domain=${CLIENT}
    ...  dest_hosts=${CLIENT_IP}
    commit

    Roll Over Now  mail_logs

Do Common Testcase Teardown
    Set Appliance Under Test to ESA
    Start CLI Session
    ${smtproute_delete_status}=  Run keyword and return status  EsaCliLibrary.Smtp Routes Delete  domain=${CLIENT}
    Run keyword if  '${smtproute_delete_status}'=='False'  EsaCliLibrary.Smtp Routes Clear
    Commit

    DefaultTestCaseTeardown

Add Content Filter
    [Arguments]  ${Content_filter_name}  ${actions}

    Content Filter Add  Incoming  ${Content_filter_name}
    ...  ${Content_filter_name}  ${actions}
    Commit Changes

    ${settings}=  Create Dictionary
    ...  Content Filters  Enable Content Filters (Customize settings)
    ...  Enable All  ${True}
    Mail Policies Edit Content Filters  incoming  default  ${settings}
    Commit Changes

Remove Content Filter
    [Arguments]  ${Content_filter_name}
    Content Filter Delete  Incoming  ${Content_filter_name}
    commit Changes

Disable URL Filtering
    ${URL_status}=  Url Filtering Is Enabled
    Run Keyword If  ${URL_status}
    ...  Url Filtering Disable
    Commit Changes

Add Url Content Filter
    [Arguments]  ${Content_filter_name}

    Url Filtering Enable  url_click_tracking=${True}
    Outbreak Filters Enable
    Commit Changes

    ${result}  ${msg}  Run Keyword And Ignore Error  Verify URL Filtering Status As Connected
    Run Keyword If   '${result}' == 'FAIL'
    ...  Verify Engine Is Updated  enrollment_client
    Verify URL Filtering Status As Connected

    ${action_value}=  Create Dictionary
    ...  CustomRange Reputation URL Min value  -10.00
    ...  CustomRange Reputation URL Max value  +10.00
    ...  Scan Type  All
    ...  Defang Reputation URL  None

    ${actions}=  Content Filter Create Actions
    ...  URL Reputation Action  ${action_value}

    Content Filter Add  Incoming  ${Content_filter_name}
    ...  ${Content_filter_name}  ${actions}
    Commit Changes

    ${settings}=  Create Dictionary
    ...  Content Filters  Enable Content Filters (Customize settings)
    ...  ${Content_filter_name}  ${True}
    Mail Policies Edit Content Filters  incoming  default  ${settings}
    Commit Changes

Remove Url Content Filter
    [Arguments]  ${Content_filter_name}

    Disable URL Filtering
    Outbreak Filters Disable

    Content Filter Delete  Incoming  ${Content_filter_name}
    commit Changes

Get Format Date Time
    [Arguments]  ${date_time}

    ${Formatted_datetime}=  Evaluate  datetime.datetime.strptime('${date_time[:-4]}', '${GETDTIME_FMT_LOCAL}')  datetime
    [return]  ${Formatted_datetime}

Get Search Date And Time
    Restart CLI Session
    ${current_datetime_str}=  Set Time Get
    ${current_datetime}=  Get Format Date Time  ${current_datetime_str}

    ${current_min}=  Set Variable  ${current_datetime.minute}
    ${current_hour}=  Set Variable  ${current_datetime.hour}
    ${day}=  Set Variable  ${current_datetime.day}
    ${month}=  Set Variable  ${current_datetime.month}
    ${year}=  Set Variable  ${current_datetime.year}

    ${next_hour_expr}=  Catenate
    ...  datetime.datetime.strptime('${current_datetime_str[:-4]}', '${GETDTIME_FMT_LOCAL}')+
    ...  datetime.timedelta(hours=1)

    ${next_hour_datetime}=  Evaluate  ${next_hour_expr}  datetime
    ${next_hour}=  Set Variable  ${next_hour_datetime.hour}
    ${next_min}=  Set Variable  ${next_hour_datetime.minute}

    ${next_hour}=  Run Keyword If  '${current_hour}' == '23'  Set Variable  ${current_hour}
    ...  ELSE  Set Variable  ${next_hour}
    ${next_min}=  Run Keyword If  '${current_hour}' == '23'  Set Variable  59
    ...  ELSE  Set Variable  ${next_min}

    ${date}=  Catenate  SEPARATOR=/  ${month}  ${day}  ${year}
    ${from_time}=  Catenate  SEPARATOR=:  ${current_hour}  ${current_min}
    ${to_time}=  Catenate  SEPARATOR=:  ${next_hour_datetime.hour}  ${next_hour_datetime.minute}

    Log Many  ${date}
    Log Many  ${from_time}
    Log Many  ${to_time}
    Log Many  ${current_hour}
    [Return]  ${date}  ${from_time}  ${to_time}
