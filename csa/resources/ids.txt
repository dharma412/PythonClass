# $Id: //prod/main/sarf_centos/resources/ids.txt#1 $

*** Settings ***
Resource        ftp.txt
Resource        http.txt

*** Variables ***
${IDS_POLICY}   IDSPolicy01
${CAT}          Cat01
${IDS_LINK}     Cisco IronPort Data Security
${WEBIMGE_CAT}  filetypes.WEB_IMAGE

*** Keywords ***
IDS Suite Setup
    Set Suite Variable  ${SSW_MODE}  P1
    #Restart HTTP Server
    DefaultTestSuiteSetup

IDS Suite Teardown
    DefaultTestSuiteTeardown

IDS Test Case Setup
    DefaultTestCaseSetup

IDS Test Case Teardown
    DefaultTestCaseTeardown
    Reset Config
    SSW Load From Config File
    Remove Test File

Filter IDS Log Create Baseline
    ${_baseline_ids_log}=  Filter Log Create Baseline  ${IDSDATALOSS_LOGS}
    Set Test Variable    ${baseline_ids_log}    ${_baseline_ids_log}

Remove Cookie File
    Run  rm ${COOKIE_FILE}

Remove Test File
    ${status}  ${value}=  Run Keyword And Ignore Error  Variable Should Exist  \${TEST_FILE}
    Run Keyword If  '${status}' == 'PASS'
    ...  Run  rm ${TEST_FILE}

Remove Test Files
    ${files}=  Catenate  @{TEST_FILES}
    Run  rm ${files}

Set NTLM Authentication
    Authentication Add Ntlm Realm   ${NTLM_REALM}
    ...                             ${NTLM_AUTH_SERVER}
    ...                             ${AD_DOMAIN}
    ...                             domain_user=${AD_JOIN_USER}
    ...                             domain_pw=${AD_JOIN_USER_PASSWORD}
    Identities Edit Policy  ${GLOBAL_IDENTITY}
    ...  auth_method=requires
    ...  auth_realm=${NTLM_REALM}
    ...  auth_scheme=Basic
    Commit Changes

Set IDS With LDAP
    Set LDAP Authentication
    Onbox Dlp Policies Add  ${IDS_POLICY}  identities=${GLOBAL_IDENTITY}:selected:${LDAP_USER}
    Onbox Dlp Policies Edit Content  ${IDS_POLICY}  blocking_settings=custom
    ...  block_types=${filetypes.MEDIA_VIDEO}:all
    Commit Changes

Set IDS With NTLM
    ids.Set NTLM Authentication
    Onbox Dlp Policies Add  ${IDS_POLICY}  identities=${GLOBAL_IDENTITY}:selected:${NTLM_USER}
    Onbox Dlp Policies Edit Content  ${IDS_POLICY}  blocking_settings=custom
    ...  block_types=${${WEBIMGE_CAT}}:all
    Commit Changes

IDS Enable
    Onbox Dlp Enable
    Log Subscriptions Add   ${log_type.dslog}  dslog  log_level=Trace
    Commit Changes

Block Custom MIME Type ${blocktype}
    Onbox Dlp Policies Edit Content  Global Policy
    ...  blocking_settings=custom
    ...  mime_types=${blocktype}
    Commit Changes

Block File Type ${blocktype}
    Onbox Dlp Policies Edit Content  Global Policy
    ...  blocking_settings=custom
    ...  block_types=${blocktype}:all
    Commit Changes

Block FileSize Type ${blocktype} If Size Greater Than ${size}
    Onbox Dlp Policies Edit Content  Global Policy
    ...  blocking_settings=custom
    ...  block_types=${blocktype}:${size}
    Commit Changes

Block HTTP Upload If Size Greater Than ${size}
    Onbox Dlp Policies Edit Content  Global Policy
    ...  blocking_settings=custom
    ...  http_file_size=${size}
    Commit Changes

Block File Based On Regex Pattern ${name_pattern}
    Onbox Dlp Policies Edit Content  Global Policy
    ...  blocking_settings=custom
    ...  regexes=${name_pattern}
    Commit Changes

Block File Based On Filename ${block_name}
    Onbox Dlp Policies Edit Content  Global Policy
    ...  blocking_settings=custom
    ...  file_names=${block_name}
    Commit Changes

Enable WBRS Without Adaptive Scanning
    WBRS and Anti Malware Edit Settings
    ...  wbrs=${True}
    ...  content_filtering=${False}
    Commit Changes

Set URL Categories Blocking
    Custom Url Category Add  ${CAT}  sites=${SERVICE_SERVER}
    @{url_cats}=  Create List  ${CAT}:block  ${webcats.IW_TRNS}:block
    Access Policies Edit Url Categories  Global Policy
    ...  url_categories=${url_cats}
    Commit Changes

Set Upstream Policy Config
    Restart delegated For http
    Identities Add Policy  ${IDENTITY}  subnet=10.0.0.0/8
    Upstream Proxy Add Group    ${UPSTREAM_GROUP}
    ...                         ${FTP_UPSTREAM_PROXY}:${FTP_UPSTREAM_PROXY_PORT}:${RECONNECTIONS}
    ...                         failure_handling=drop
    Routing Policies Add    ${R_POLICY}
    ...                     identities=${IDENTITY}
    Routing Policies Edit Destination  ${R_POLICY}  ${UPSTREAM_GROUP}
    Commit Changes

Navigate To IDS Page
    Mouse Over  link=Web Security Manager
    Click Link  ${IDS_LINK}

Setup Custom IDS Policy That Inherits Global Policy
    Identities Add Policy  ${IDENTITY}  subnet=10.0.0.0/8
    Onbox Dlp Policies Add  ${IDS_POLICY}  identities=${IDENTITY}
    Onbox Dlp Policies Edit Content  ${IDS_POLICY}  blocking_settings=global
    Commit Changes

Delete Ids Policy
    Onbox DLP Policies Delete   ${IDS_POLICY}
    Commit Changes

Configure Onbox IDS To Block By Name
    Onbox DLP Policies Edit Content     Global Policy
    ...     blocking_settings=custom
    ...     file_names=${TEST_FILE}
    Commit Changes

Configure Onbox IDS To Block By Size
    Onbox DLP Policies Edit Content     Global Policy
    ...     blocking_settings=custom
    ...     ftp_file_size=10 mb
    Commit Changes

Revert IDS Configuration
    Onbox DLP Policies Edit Content     Global Policy
    ...     blocking_settings=custom
    ...     file_names=${EMPTY}
    ...     ftp_file_size=no_limit
    Commit Changes

Set Up Onbox Dlp Policy Http Size Limit To ${limit}
    Onbox DLP Policies Edit Content     Global Policy
    ...     blocking_settings=custom
    ...     http_file_size=${limit}
    Commit Changes
